&НаКлиенте
Процедура ПоказатьКарту(Команда)
	
	Координаты = ПолучитьКоординатыКлиента();
	
	Если Координаты <> Неопределено Тогда
		
		#Если МобильноеПриложениеКлиент Тогда
			
			Если ПоддерживаетсяОтображениеКарты() Тогда
				
				ПоказатьНаКарте(Координаты);
				
			Иначе

				ВывестиПредупреждение("Не поддерживается отображение карты!");
				
			КонецЕсли;
		
		#КонецЕсли

		
	Иначе
		
		ВывестиПредупреждение("Не заполнены поля, описывающие адрес клиента!");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиПредупреждение(ТекстСообщения) Экспорт
	ПоказатьПредупреждение(, НСтр("ru='" + ТекстСообщения + "'"));
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКоординатыКлиента()
	
	 Координаты = Неопределено;
	 #Если МобильноеПриложениеКлиент Тогда
			
		Если Объект.Широта <> 0 ИЛИ Объект.Долгота <> 0 Тогда
				
			Координаты = Новый ГеографическиеКоординаты(Объект.Широта, Объект.Долгота);	
				
		Иначе
			
			СтруктураДанныхАдреса = Новый Структура();
			СтруктураДанныхАдреса.Вставить("Страна",Объект.Страна);
			СтруктураДанныхАдреса.Вставить("Город", Объект.Город);
			СтруктураДанныхАдреса.Вставить("Улица", Объект.Улица);
			СтруктураДанныхАдреса.Вставить("Дом", Объект.Дом);
			ДанныеАдреса = Новый ДанныеАдреса(СтруктураДанныхАдреса);
			Координаты = ПолучитьМестоположениеПоАдресу(ДанныеАдреса);
			
		КонецЕсли;
		
	#КонецЕсли
	
	Возврат Координаты;
	 
	
КонецФункции

&НаКлиенте
Процедура ИспользоватьТекущееМестоположение()
	
	#Если МобильноеПриложениеКлиент Тогда
			
    Провайдер = СредстваГеопозиционирования.ПолучитьСамогоЭнергоЭкономичногоПровайдера();
	Если СредстваГеопозиционирования.ОбновитьМестоположение(Провайдер.Имя, 60) Тогда
		ДанныеМестоположения = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(Провайдер.Имя);
		ДанныеАдреса = ПолучитьАдресПоМестоположению(ДанныеМестоположения.Координаты);
		
		Если ДанныеАдреса <> Неопределено Тогда
			
			Объект.Страна = ДанныеАдреса.Страна;
			Объект.Город = ДанныеАдреса.Город;
			Объект.Улица = ДанныеАдреса.Улица;
			Объект.Дом = ДанныеАдреса.Дом;
			Объект.Широта = ДанныеМестоположения.Координаты.Широта;
			Объект.Долгота = ДанныеМестоположения.Координаты.Долгота;
			Модифицировать = Истина;
		
		Иначе
						
			ВывестиПредупреждение("Не удалось установить адрес по местоположению!");
			
	    КонецЕсли;
		
	Иначе
		
		ВывестиПредупреждение("Не удалось установить адрес по местоположению!");
		
	КонецЕсли;
		
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ПроложитьМаршрут(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		
		  ДанныеМестоположения = Неопределено;
		  Провайдер = СредстваГеопозиционирования.ПолучитьСамогоЭнергоЭкономичногоПровайдера();
		  Если СредстваГеопозиционирования.ОбновитьМестоположение(Провайдер.Имя, 60) Тогда
			  ДанныеМестоположения = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(Провайдер.Имя);
		  КонецЕсли;
		  
		  Если ДанныеМестоположения = Неопределено Тогда
			  
				ВывестиПредупреждение("Не удалось установить текущее местоположение!");
 
		  КонецЕсли;
		  
		  КоординатыКлиента = ПолучитьКоординатыКлиента();
		  Если КоординатыКлиента = Неопределено Тогда
			  
				ВывестиПредупреждение("Не удалось определить расположение клиента!");
 
		  КонецЕсли;
			
		  Запуск = Новый ЗапускПриложенияМобильногоУстройства("android.intent.action.VIEW", "http://maps.google.com/maps?saddr="
		  + XMLСтрока(ДанныеМестоположения.Координаты.Широта) + "," + XMLСтрока(ДанныеМестоположения.Координаты.Долгота)
		  + "&daddr=" + XMLСтрока(КоординатыКлиента.Широта) + "," + XMLСтрока(КоординатыКлиента.Долгота));
		  Запуск.Запустить(Ложь);

		
	#КонецЕсли

КонецПроцедуры